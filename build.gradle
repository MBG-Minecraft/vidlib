plugins {
	id 'base'
	id 'java'
	id 'idea'
	id 'maven-publish'
	id 'net.neoforged.moddev' version "2.0.75"
}

var ENV = System.getenv()
var localBuildFile = new File(project.rootDir, "build.txt")
var localBuildNumber = localBuildFile.getText("UTF-8").toInteger() + 1

version = "${mod_version}-build.$localBuildNumber"
group = project.maven_group
base.archivesBaseName = project.archives_base_name

println("Building version: ${version}")

neoForge {
	version = project.neoforge_version
	// validateAccessTransformers = true // See https://github.com/neoforged/ModDevGradle/issues/236

	accessTransformers {
		publish(project.file('src/main/resources/META-INF/accesstransformer.cfg'))
	}

	interfaceInjectionData {
		from(file('interfaces.json'))
		publish(file('interfaces.json'))
	}

	runs {
		configureEach {
			logLevel = org.slf4j.event.Level.INFO
		}

		client {
			ideName = 'Client'
			client()
			systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id

			if (ENV.MC_CLIENT_ARGS) {
				programArguments.addAll(ENV.MC_CLIENT_ARGS.split(' '))
			}

			jvmArguments.addAll("-XX:+IgnoreUnrecognizedVMOptions", "-XX:+AllowEnhancedClassRedefinition")
			gameDirectory = file 'run'
		}

		client2 {
			ideName = 'Client 2'
			client()
			systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id

			if (ENV.MC_CLIENT_2_ARGS) {
				programArguments.addAll(ENV.MC_CLIENT_2_ARGS.split(' '))
			}

			jvmArguments.addAll("-XX:+IgnoreUnrecognizedVMOptions", "-XX:+AllowEnhancedClassRedefinition")
			gameDirectory = file 'run_2'
		}

		server {
			ideName = 'Server'
			server()
			programArgument("--nogui")
			gameDirectory = file 'run_server'
		}
	}

	mods {
		shimmer {
			sourceSet sourceSets.main
		}
	}
}

compileJava {
	options.encoding = "UTF-8"
	options.release.set(21)
	options.compilerArgs << '-parameters' << '-Xmaxerrs' << '1000'
}

java {
	sourceCompatibility = targetCompatibility = '21'
	withSourcesJar()
}

configurations {
	runtimeClasspath.extendsFrom localRuntime
}

repositories {
	mavenLocal()
	mavenCentral()

	maven {
		url "https://maven.neoforged.net/releases"
	}

	maven {
		name = 'ParchmentMC'
		url = 'https://maven.parchmentmc.org'
		content {
			includeGroup "org.parchmentmc.data"
		}
	}

	maven {
		url = 'https://api.modrinth.com/maven'
		content {
			includeGroup("maven.modrinth")
		}
	}
}

dependencies {
	compileOnly "maven.modrinth:simple-voice-chat:QOjXGg4I"
}

processResources {
	def toReplace = [
			"version": project.version
	]

	println("[Process Resources] Replacing properties in resources: " + toReplace)

	inputs.properties toReplace
	filesMatching("META-INF/neoforge.mods.toml") {
		expand toReplace
	}
}

jar {
	manifest {
		attributes([
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

publishing {
	publications {
		mavenNeoForge(MavenPublication) {
			artifactId = archives_base_name
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_URL && ENV.MAVEN_USERNAME && ENV.MAVEN_TOKEN) {
			maven {
				url = ENV.MAVEN_URL
				credentials {
					username = ENV.MAVEN_USERNAME
					password = ENV.MAVEN_TOKEN
				}
			}
		}

		/*
		if (ENV.MBG_MAVEN_URL && ENV.MBG_MAVEN_USERNAME && ENV.MBG_MAVEN_TOKEN) {
			maven {
				url = ENV.MBG_MAVEN_URL
				credentials {
					username = ENV.MBG_MAVEN_USERNAME
					password = ENV.MBG_MAVEN_TOKEN
				}
			}
		}
		 */
	}
}

idea {
	module {
		downloadSources = true
		downloadJavadoc = true
	}
}

tasks.register('increaseLocalBuild') {
	group 'build'
	description 'Increase local build number'

	doFirst {
		localBuildFile.setText(localBuildNumber.toString(), "UTF-8")
	}
}

tasks.register('releaseLocalBuild') {
	group 'build'
	description 'Builds the mod locally'
	dependsOn tasks.increaseLocalBuild
	dependsOn tasks.build
	dependsOn tasks.publishToMavenLocal

	if (ENV.MAVEN_URL && ENV.MAVEN_USERNAME && ENV.MAVEN_TOKEN) {
		dependsOn tasks.publish
	}
}