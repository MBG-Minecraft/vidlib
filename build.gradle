import java.net.http.HttpClient
import java.net.http.HttpRequest
import java.net.http.HttpResponse

plugins {
	id 'base'
	id 'java'
	id 'idea'
	id 'maven-publish'
	id 'net.neoforged.moddev' version "2.0.137"
}

var ENV = System.getenv() + (file('.env').exists() ? file('.env').readLines().findAll { it && !it.startsWith('#') }.collectEntries {
	def (k, v) = it.split('=', 2); [(k.trim()): v.trim()]
} : [:])

var CI = ENV.CI == "true"

var localBuildFile = new File(project.rootDir, "build.txt")
var localBuildNumber = localBuildFile.getText("UTF-8").toInteger() + 1
var webHookId = ""

version = "$mod_version-build.$localBuildNumber"
group = project.maven_group
base.archivesName = project.archives_base_name

println("Building version: $version")

neoForge {
	enable {
		version = project.neoforge_version
		validateAccessTransformers = !CI
		disableRecompilation = CI

		accessTransformers {
			publish(project.file('src/main/resources/META-INF/accesstransformer.cfg'))
		}

		interfaceInjectionData {
			from(file('interfaces.json'))
			publish(file('interfaces.json'))
		}

		runs {
			configureEach {
				logLevel = org.slf4j.event.Level.INFO
			}

			client {
				ideName = 'Client'
				client()
				systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id

				if (ENV.MC_CLIENT_ARGS) {
					programArguments.addAll(ENV.MC_CLIENT_ARGS.split(' '))
				}

				jvmArguments.addAll("-XX:+IgnoreUnrecognizedVMOptions", "-XX:+AllowEnhancedClassRedefinition")
				gameDirectory = file 'runs/client'
			}

			client2 {
				ideName = 'Client 2'
				client()
				systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id

				if (ENV.MC_CLIENT_2_ARGS) {
					programArguments.addAll(ENV.MC_CLIENT_2_ARGS.split(' '))
				}

				jvmArguments.addAll("-XX:+IgnoreUnrecognizedVMOptions", "-XX:+AllowEnhancedClassRedefinition")
				gameDirectory = file 'runs/client'
			}

			server {
				ideName = 'Server'
				server()
				programArgument("--nogui")
				gameDirectory = file 'runs/server'
			}
		}

		mods {
			vidlib {
				sourceSet sourceSets.main
			}
		}
	}
}

compileJava {
	options.encoding = "UTF-8"
	options.release.set(21)
	options.compilerArgs << '-parameters' << '-Xmaxerrs' << '1000'
}

java {
	sourceCompatibility = targetCompatibility = '21'
	withSourcesJar()
	// withJavadocJar()
}

configurations {
	runtimeClasspath.extendsFrom localRuntime
}

tasks.withType(Javadoc) {
	options.addStringOption('Xdoclint:none', '-quiet')
}

repositories {
	mavenLocal()
	mavenCentral()

	maven {
		url "https://maven.neoforged.net/releases"
	}

	maven {
		url = 'https://maven.parchmentmc.org'
		content {
			includeGroup "org.parchmentmc.data"
		}
	}

	if (findProperty('mrbeastURL') ?: ENV.MBG_MAVEN_URL) {
		maven {
			url = findProperty('mrbeastURL') ?: ENV.MBG_MAVEN_URL
			content {
				includeGroup "software.bernie.geckolib"
				includeGroup "dev.latvian.mods"
				includeGroup "dev.latvian.apps"
				includeGroup "dev.beast.mods"
				includeGroup "maven.modrinth"
			}

			credentials {
				username = findProperty('mrbeastUsername') ?: ENV.MBG_MAVEN_USERNAME
				password = findProperty('mrbeastPassword') ?: ENV.MBG_MAVEN_TOKEN
			}
		}
	}

	// Leave in the repositories below due to public repository of Vidlib

	maven {
		url = 'https://maven.latvian.dev/releases'
		content {
			includeGroup "dev.latvian.mods"
			includeGroup "dev.latvian.apps"
		}
	}

	maven {
		url = 'https://api.modrinth.com/maven'
		content {
			includeGroup "maven.modrinth"
		}
	}

	maven {
		url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
		content {
			includeGroup "software.bernie.geckolib"
		}
	}
}

dependencies {
	jarJar(implementation('dev.latvian.mods:klib')) {
		version {
			strictly "[$klib_version,)"
			prefer "$klib_version"
		}
	}

	jarJar(implementation('dev.latvian.mods:better-advanced-tooltips')) {
		version {
			strictly "[$bat_version,)"
			prefer "$bat_version"
		}
	}

	interfaceInjectionData("dev.latvian.mods:klib:$klib_version")

	compileOnly("dev.latvian.mods:imgui-resources:$imgui_resources_version")

	compileOnly("maven.modrinth:simple-voice-chat:neoforge-1.21.5-2.5.36")
	compileOnly("software.bernie.geckolib:geckolib-neoforge-${minecraft_version}:${geckolib_version}")

	implementation "dev.latvian.apps:tiny-java-server:1.0.0-build.33"
}

processResources {
	def toReplace = [
			"version": project.version
	]

	println("[Process Resources] Replacing properties in resources: " + toReplace)

	inputs.properties toReplace
	filesMatching("META-INF/neoforge.mods.toml") {
		expand toReplace
	}
}

jar {
	manifest {
		attributes([
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

tasks.register('buildResourcePack', Zip) {
	group "build"
	description "Builds a resource pack from assets"
	dependsOn tasks.named('processResources')
	into('/')

	from('src/main/resources') {
		include 'assets/**'
		include 'pack.mcmeta'
		exclude '**/.DS_Store', '**/Thumbs.db'
		exclude 'assets/*/particles/**'
		exclude 'assets/*/shaders/**'
		exclude 'assets/*/post_effect/**'
		exclude 'assets/*/vidlib/canvas/**'
	}

	from('src/main/resources') {
		include 'vidlib_logo.png'
		rename { fileName ->
			fileName == 'vidlib_logo.png' ? 'pack.png' : fileName
		}
	}

	destinationDirectory = layout.buildDirectory.dir('libs')
	archiveBaseName.set(project.name)
	archiveVersion.set(project.version.toString())
	archiveClassifier.set('resource-pack')

	preserveFileTimestamps = false
	reproducibleFileOrder = true
	duplicatesStrategy = DuplicatesStrategy.WARN
}

tasks.build {
	dependsOn tasks.buildResourcePack
}

publishing {
	publications {
		mavenNeoForge(MavenPublication) {
			artifactId = archives_base_name
			from components.java

			artifact(tasks.named('buildResourcePack')) {
				classifier 'resource-pack'
				extension 'zip'
			}
		}
	}

	repositories {
		if (ENV.MAVEN_URL && ENV.MAVEN_USERNAME && ENV.MAVEN_TOKEN) {
			maven {
				url = ENV.MAVEN_URL
				credentials {
					username = ENV.MAVEN_USERNAME
					password = ENV.MAVEN_TOKEN
				}
			}
		}
	}
}

idea {
	module {
		if (!CI) {
			downloadSources = true
			downloadJavadoc = true

			["client", "server"].each {
				[
						".architectury-transformer",
						"config",
						"crash-reports",
						"debug",
						"downloads",
						"dumps",
						"flashback",
						"local",
						"logs",
						"mods",
						"profilekeys",
						"saves",
						"voicechat_recordings",
						"command_history.txt",
						"options.txt",
						"server.properties",
						"world",
						"emi.json",
						"usercache.json",
						"usernamecache.json"
				].each { ext ->
					excludeDirs.add(file("runs/$it/$ext"))
				}
			}
		}
	}
}

tasks.register('increaseLocalBuild') {
	group 'other'
	description 'Increase local build number'

	doFirst {
		localBuildFile.setText(localBuildNumber.toString(), "UTF-8")
	}
}

tasks.register('webHookUpdate') {
	group 'other'
	description 'Send webhook update'

	def content = "New VidLib version: [`${version}`](<${ENV.MAVEN_ARTIFACT_LOCATION}/${version}>) (KLib `${klib_version}`)"

	doLast {
		println("Sending webhook update...")
		// localBuildFile.setText(localBuildNumber.toString(), "UTF-8")

		def client = HttpClient.newHttpClient()

		def request = HttpRequest.newBuilder()
				.uri(URI.create("${ENV.DISCORD_WEBHOOK_URL}"))
				.header("Content-Type", "application/json")
				.POST(HttpRequest.BodyPublishers.ofString(
						'{"content":"' + content + '"}'
				))
				.build()

		def response = client.send(request, HttpResponse.BodyHandlers.ofString())
		println "Status code: ${response.statusCode()}"
		println "Response body: ${response.body()}"
	}
}

tasks.register('releaseLocalBuild') {
	group 'publishing'
	description 'Builds the mod locally'
	dependsOn tasks.increaseLocalBuild
	dependsOn tasks.build
	dependsOn tasks.publishToMavenLocal

	if (ENV.MAVEN_URL && ENV.MAVEN_USERNAME && ENV.MAVEN_TOKEN) {
		println("Publishing enabled, ${ENV.MAVEN_USERNAME} to ${ENV.MAVEN_URL}")
		dependsOn tasks.publish
	} else {
		println("Publishing disabled")
	}

	if (ENV.DISCORD_WEBHOOK_URL) {
		dependsOn tasks.webHookUpdate
	}
}